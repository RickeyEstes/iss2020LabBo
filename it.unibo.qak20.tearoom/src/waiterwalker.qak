/* 
 * ================================================================================
 * maitrewalker.qak
 * VERSION June 1, 2020 
 * 
 * External MQTT sites:
 * 	"mqtt.eclipse.org:  mosquitto.org mqtt.fluux.io  broker.hivemq.com
 * ================================================================================
 */ 
System   tearoom 
mqttBroker "mqtt.eclipse.org" : 1883 eventTopic "unibo/polar" 

Event    twstarted   : twstarted(X)
	
Request  movetoCell : movetoCell(X,Y)
Reply    atcell     : atcell(X,Y)
Reply	 walkbreak  : walkbreak(X,Y) 

Dispatch cmd       : cmd(MOVE)
Request step       : step( TIME )	
Reply   stepdone   : stepdone(V)  
Reply   stepfail   : stepfail(DURATION, CAUSE)

Context ctxtearoom        ip [host="localhost"      port=8050]     
Context ctxbasicrobot     ip [host="192.168.1.68"   port=8020]   

ExternalQActor basicrobot    context ctxbasicrobot
 
QActor maitrewalker context ctxtearoom{  
[#
	var XT = "0"
	var YT = "0"
	var CurrentPlannedMove = ""
	var StepTime    	   = 348L
	val BackTime           = 2 * StepTime / 3
	var obstacleFound      = false  
#]

	State s0 initial {	     
 		forward basicrobot -m cmd : cmd(l)
		delay 500
		forward basicrobot -m cmd : cmd(r)    
		delay 500
		emit twstarted : twstarted(0)
		println("&&&  maitrewalker STARTS")
	}	
	Goto waitCmd   
	 
	State waitCmd {	     
 		println("&&&  maitrewalker waits for a command 'movetoCell'")
	}	
	Transition t0 whenRequest movetoCell -> walk
 	
	State walk{
		printCurrentMessage
		onMsg( movetoCell : movetoCell(X,Y) ){
			[# XT = payloadArg(0)
			   YT = payloadArg(1)			  
			 #]
			println("&&&  maitrewalker  | MOVING to ($XT,$YT)")
			run itunibo.planner.plannerUtil.planForGoal("$XT","$YT")
		}
	}
	Goto execPlannedMoves if[# itunibo.planner.plannerUtil.existActions() #] else noPlan

	State noPlan{
		println("&&&  maitrewalker | NO PLAN FOUND for MOVING to ($XT,$YT)")
		replyTo movetoCell with walkbreak : walkbreak( $XT,$YT )
	}
	Goto waitCmd
	
/*
 * Execute the plan
 */					  
 	State execPlannedMoves{ 
 		[#  CurrentPlannedMove = itunibo.planner.plannerUtil.getNextPlannedMove() #]
 		//println("maitrewalker | doing move $CurrentPlannedMove")
 	}
 	Goto wMove if [# CurrentPlannedMove == "w" #] else otherPlannedMove
 
 	State wMove {  
		request basicrobot -m step : step( $StepTime  )
 	} 	
 	Transition t0   whenReply stepdone   -> stepDone   
					whenReply stepfail   -> stepFailed 
	 
 	State stepDone{  
 		updateResource [# itunibo.planner.plannerUtil.getMapOneLine() #]
 		run itunibo.planner.plannerUtil.updateMap(  "w" )
  	}
	Goto execPlannedMoves if [# CurrentPlannedMove.length > 0 #] else sendSuccessAnswer
		
	State stepFailed{  //has found an obstacle 
	    [# obstacleFound = true #]
 		println("maitrewalker | stepFailed")
		onMsg(stepfail : stepfail(DURATION, CAUSE) ){
			[# val D = payloadArg(0).toLong()  ; val Dt = Math.abs(StepTime-D); val BackT = D/2 #] 
			println("maitrewalker stepFail D= $D, BackTime = ${BackTime}")
			if [# D > BackTime #] {    
				forward basicrobot -m cmd : cmd( s )
				delayVar BackT
				forward basicrobot -m cmd : cmd( h )
			}	 
 		}
	    run itunibo.planner.plannerUtil.updateMapObstacleOnCurrentDirection() 			    
 	}   
	Goto sendFailureAnswer
	
 	State otherPlannedMove{
  		if [# CurrentPlannedMove == "l" || CurrentPlannedMove == "r"  #]{
  			forward basicrobot -m cmd : cmd($CurrentPlannedMove)
 			run itunibo.planner.plannerUtil.updateMap( "$CurrentPlannedMove" )
  		} 
 	}   
 	Goto execPlannedMoves if [# CurrentPlannedMove.length > 0 #] else sendSuccessAnswer 
 
/*
 * Answers  
 */ 
 	State sendSuccessAnswer{
	     	println("&&&  maitrewalker POINT ($XT,$YT) REACHED")
	     	run itunibo.planner.plannerUtil.showCurrentRobotState()
	     	replyTo movetoCell with atcell : atcell($XT,$YT) 					
	}
	Goto waitCmd  

	State sendFailureAnswer{
	    println("&&&  maitrewalker FAILS")
	    [# val Curx = itunibo.planner.plannerUtil.getPosX()
	       val Cury = itunibo.planner.plannerUtil.getPosY()	
	    #]
  		run itunibo.planner.plannerUtil.showCurrentRobotState()
	    replyTo movetoCell with walkbreak : walkbreak( $Curx, $Cury ) 					
	}
	Goto waitCmd
       
 }
 
