System   domains
mqttBroker "localhost" : 1883 eventTopic "unibo/polar" 	//mqtt.eclipse.org
Event    walkerstarted   : walkerstarted(X)	

//---------- Walker---------------
Request  movetoCell : movetoCell(X,Y)
Reply    atcell     : atcell(X,Y)
Reply	 walkbreak  : walkbreak(X,Y) 
Dispatch rest       : rest(V)


//---------- Tearoom ---------------
Request getroomstate : getroomstate(V)
Reply   roomstate    : roomstate(Answer)

//---------- Client ---------------
Request enterrequest : enterrequest(ID)
Reply   enteranswer  : enteranswer(V)

Context ctxdomains  ip [host="localhost" port=8060]   

CodedQActor waiterwalker context ctxdomains className "it.unibo.waiterwalker.Waiterwalker"
 
QActor waiter context ctxdomains{
[# var CurrentClientId        	= 0
   var CurrentSelectedTable   	= 0
   var CurMoveX  			  	= 0
   var CurMoveY  				= 0
   
   val inmapname          		= "teaRoomExplored" 
#]	

	State s0 initial {	
  		run itunibo.planner.plannerUtil.initAI()
		solve( consult("tearoomkb.pl")	 )
  		
 		run itunibo.planner.plannerUtil.loadRoomMap( inmapname ) 		
 		run itunibo.planner.plannerUtil.showCurrentRobotState()		
 		
 		updateResource [# "waiter athome" #]		 
 	}	 
  	Transition t0 whenEvent walkerstarted -> someQuery //controlTrip    
 	
 	 
 	State someQuery{
  		println("waiter | sending some query ... ")
 			solve( teatable(1,ST)  )  
			ifSolved { [# val S = getCurSol("ST").toString()   #] 
				println("waiter | teatable 1 state=$S") 
 			}					
			solve( numavailabletables(N) ) 
			ifSolved { [# val Ntables = getCurSol("N").toString().toInt()  #] 
				println("waiter | numavailabletables=$Ntables")
 			}					
			solve( roomstate( V )  )   
			ifSolved { 
				[# val W = getCurSol("V").toString()   
				#] 
				println("waiter | $W  ") 				
			}
		println(" ------------- end of Queries ----------------  ")	
	}	
	Goto listening
	
	State listening{
		[#  //RESET all variables
			CurrentClientId        	= 0
			CurrentSelectedTable   	= 0
			CurMoveX  			  	= 0
			CurMoveY  				= 0		
			var CurX                = itunibo.planner.plannerUtil.getPosX()
			var CurY                = itunibo.planner.plannerUtil.getPosY()	
			var Msg                 = "waiter listening at ($CurX,$CurY)"
		#]
 		updateResource [# Msg #]
 		println( Msg)
	}
  	Transition t0  
  			whenTime 5000 -> checkrest 		 
  			whenRequest enterrequest -> handleEnterrequest
  
    
	State checkrest  {
		[# var Msg = "waiter moving to rest" #]
		if [# ! itunibo.planner.plannerUtil.atHome() #] {
			updateResource [# Msg #]
 			request waiterwalker -m movetoCell : movetoCell (0,0)
  		} 
		else{ forward waiter -m rest : rest(0)  }
	}
	Transition t0 whenMsg rest 		  ->  listening
				  whenReply atcell    ->  listening
				  whenReply walkbreak -> unexpected
	
 	
	State handleEnterrequest{
 		printCurrentMessage
 			onMsg( enterrequest : enterrequest(CID) ){
 				[# CurrentClientId = payloadArg(0).toString().toInt() #]
				solve( tableavailable(N) ) 
				ifSolved { [# CurrentSelectedTable = getCurSol("N").toString().toInt()  #] 
					println("waiter | tableavailable=$CurrentSelectedTable")					 
					replyTo enterrequest with enteranswer : enteranswer( $CurrentSelectedTable )
					solve( engageTable($CurrentSelectedTable, $CurrentClientId ) ) 
	 			}else{
	 				replyTo enterrequest with enteranswer : enteranswer( 0 )
	 				[# CurrentClientId = 0 #]  //no client
	 			}		  
 		}					
 	}
 	Goto listening if [# CurrentClientId == 0 #] else gotoEntrance
 	
 	State gotoEntrance{
 		updateResource [# "going to entrace for client $CurrentClientId" #]
 		println("waiter | going to entracedoor for client = $CurrentClientId ")	
 		solve( pos( entrancedoor, X,Y ) )
  		ifSolved { 	//defensive
 			[# CurMoveX = getCurSol("X").toString().toInt()  
 			   CurMoveY = getCurSol("Y").toString().toInt()  
 			#]  
 			request waiterwalker -m movetoCell : movetoCell($CurMoveX, $CurMoveY)
 		}	
 	}
 	Transition t0 whenReply atcell    -> deployCurrentclient
 				  whenReply walkbreak -> unexpected
 	
 	State deployCurrentclient{
 		updateResource [# "waiter at entrace, depolying client $CurrentClientId to table$CurrentSelectedTable " #]
 		//1) move to entrancedoor 2) move to the CurrentSelectedTable
 		solve( pos( teatable, $CurrentSelectedTable, X, Y ) )
 		ifSolved { 	//defensive
 			[# CurMoveX = getCurSol("X").toString().toInt()  
 			   CurMoveY = getCurSol("Y").toString().toInt() - 1  //cell upon the table
 			#]  
 			request waiterwalker -m movetoCell : movetoCell($CurMoveX, $CurMoveY)
 		}
 	}
 	Transition t0 whenReply atcell    -> clientdeployed
	 			  whenReply walkbreak -> unexpected

	State clientdeployed{
		[# val Msg = "DEPLOYED client $CurrentClientId to table$CurrentSelectedTable at ($CurMoveX, $CurMoveY)"#]
		updateResource [# Msg #]
		println("waiter | $Msg")	
 		[# readLine() #] 
 		//delay 2000
	}
  	Goto listening

	 			  
    State endOfJob{
    	run itunibo.planner.plannerUtil.showCurrentRobotState()
    	println("END OF Job ... BYE")
    }   
    
 	State unexpected{
 		println("Sorry, there is something wrong ...")
		printCurrentMessage
	}
}


QActor clientsimulator context ctxdomains{
	[# var ClientId = 1 #]
	State s0 initial {		
		delay 2000	
	}
	Goto dorequest
	
	State dorequest{
		 request  waiter -m enterrequest : enterrequest( $ClientId )
		 [# ClientId = ClientId+1 #]
	}
	Transition t0  whenReply enteranswer -> handleEnteranswer
	
	State handleEnteranswer{
		printCurrentMessage
 	}
	Goto dorequest if [# ClientId < 4 #] else endOfJob
	
    State endOfJob{
     	println("clientsimulator BYE")
    }   
	
}